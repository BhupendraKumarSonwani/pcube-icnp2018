/* Copyright 2018-present IIT Bombay (Akash Trehan, Huzefa Chasmai, Aniket Shirke)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// ------------------ Constants --------------------

#define UPPER_LIMIT 80
#define LOWER_LIMIT 20
#define SERVERS 2
#define SERVERPLUSONE 3
#define SWITCHES 12
#define SWITCHMINUSONE 11
#define MCAST_GRP 1
#define THRESHOLD 99
#define SQESHOLD 10000

// -------------------- Headers ------------------------

header_type load_balancer_t {
    fields {
        preamble: 64;
        syn: 32;
        fin: 32;
        fid: 32;
        subfid : 32;
        packet_id : 32;
        hash : 32;
        _count : 32;
    }
}
header load_balancer_t load_balancer_head;

header_type sync_info_t {
    fields {
        flow_num: 32;
    }
}
header sync_info_t sync_info_head;

header_type meta_t {
    fields {
        temp : 32;

        @for (i) (1,SERVERPLUSONE,1)
            server_flow$i : 32;
        @endfor

        @for (i) (1,SWITCHES,1)
            switch_flow$i : 32;
        @endfor

        hash: 16;
        routing_port: 32;

        min_flow_len : 32;

        probe_bool : 32;
    }
}
metadata meta_t meta;

header_type intrinsic_metadata_t {
    fields {
        mcast_grp : 16;
        egress_rid : 16;
    }
}
metadata intrinsic_metadata_t intrinsic_metadata;

// ---------------------- Hashing ------------------

field_list flow_list {
    load_balancer_head.fid;
}

field_list_calculation flow_register_index {
    input {
        flow_list;
    }
    algorithm : crc16;
    output_width : 16;
}

//-------------------- Parsers ------------------------

parser start {
    return select(current(0, 64)) {
        0: parse_head;
        1: interswitch;
        default: ingress;
    }
}

parser parse_head {
    extract(load_balancer_head);
    extract(sync_info_head);
    return ingress;
}

parser interswitch {
    extract(load_balancer_head);
    extract(sync_info_head);
    return ingress;
}

//------------------ Registers -----------------------

register total_flow_count_register {
    width: 32;
    instance_count: SERVERS + SWITCHES;
}

register flow_to_port_map_register {
    width: 32;
    instance_count: 65536;
}

// --------------------- Tables -------------------------------

table get_server_flow_count_table{
    actions{
        get_server_flow_count;
    }
    size:1;
}

@for (i) (1,SERVERPLUSONE,1)
    table update_min_flow_len$i_table1 {
        actions{
            update_min_flow_len$i;
        }
        size: 1;
    }
@endfor

table send_update_table {
    actions{
        send_update;
    }
    size: 1;
}

table update_switch_flow_count_table {
    actions{
        update_switch_flow_count;
    }
    size: 1;
}

table set_server_dest_port_table{
    reads{
        @for (i) (1,SERVERPLUSONE,1)
            meta.server_flow$i : exact;
        @endfor
    }
    actions{
        set_server_dest_port;
    }
    size:SQESHOLD;
}

table set_probe_bool_table {
    actions{
        set_probe_bool;
    }
    size: 1;
}

table get_switch_flow_count_table{
    actions{
        get_switch_flow_count;
    }
    size:1;
}

@for (i) (1,SWITCHES,1)
    table set_switch$i_dest_port_table{
        actions{
            set_switch$i_dest_port;
        }
        size:1;
    }
@endfor

table update_map_table {
    actions {
        update_map;
    }
    size: 1;
}

table forwarding_table {
    reads{
        meta.routing_port: valid;
    }
    actions{
        forward;
        _drop;
    }
    size: 1;
}

table drop_table {
    actions {
        _drop;
    }
    size: 1;
}

table send_probe_table {
    actions{
        send_probe;
    }
    size: 1;
}

table clear_map_table {
    actions {
        clear_map;
    }
    size: 1;
}

table update_flow_count_table {
    actions{
        update_flow_count;
    }
    size: 1;
}

@for (i) (1,SERVERPLUSONE,1)
    table update_min_flow_len$i_table2 {
        actions{
            update_min_flow_len$i;
        }
        size: 1;
    }
@endfor

table send_broadcast_update_table {
    actions{
        send_broadcast_update;
    }
    size: 1;
}

// ---------------------------- Actions -----------------------

action get_server_flow_count(){

    @for (i) (1,SERVERPLUSONE,1)
        register_read(meta.server_flow$i,total_flow_count_register,$i - 1);
    @endfor
}

@for (i) (1,SERVERPLUSONE,1)
    action update_min_flow_len$i(){
        modify_field(meta.min_flow_len, meta.server_flow$i);
    }
@endfor

//--->Will be generated as _mirror_1() in a separate file
action send_update(){
    modify_field(load_balancer_head.preamble,2);
    modify_field(sync_info_head.flow_num,(@sum (1, SERVERPLUSONE, 1)(meta.server_flow$i));
    modify_field(standard_metadata.egress_spec, standard_metadata.ingress_port);
}

action update_switch_flow_count() {
    register_write(total_flow_count_register,standard_metadata.ingress_port - 2, sync_info_head.flow_num);
}

action set_server_dest_port(flow_count,flow_dest){
    register_write(total_flow_count_register,flow_dest - 2, flow_count + 1);
    modify_field(standard_metadata.egress_spec,flow_dest);
}

action set_probe_bool(){
    modify_field(meta.probe_bool,1);
}

action get_switch_flow_count(){
    @for (i) (1,SWITCHES,1)
        register_read(meta.switch_flow$i,total_flow_count_register,$i + SWITCHES - 1);
    @endfor
}

@for (i) (1,SWITCHES,1)
    action set_switch$i_dest_port(){
        register_write(total_flow_count_register,$i + SWITCHES - 1, meta.switch_flow$i + 1);
        modify_field(standard_metadata.egress_spec,$i + SWITCHES + 1);
    }
@endfor

action update_map() {
    modify_field_with_hash_based_offset(meta.hash, 0,
                                        flow_register_index, 65536);
    register_write(flow_to_port_map_register, meta.hash, standard_metadata.egress_spec);
}

action forward() {
    modify_field_with_hash_based_offset(meta.hash, 0,
                                        flow_register_index, 65536);
    register_read(meta.routing_port, flow_to_port_map_register, meta.hash);
    modify_field(standard_metadata.egress_spec, meta.routing_port);
    modify_field(load_balancer_head.hash,meta.hash);
}

field_list meta_list {
    meta;
    standard_metadata;
    intrinsic_metadata;
}

//--->Will be generated as _sync_1() in a separate file
action send_probe(){
    clone_ingress_pkt_to_egress(standard_metadata.egress_spec,meta_list);
    modify_field(load_balancer_head.preamble, 1);
    modify_field(intrinsic_metadata.mcast_grp, 1);
}

action clear_map() {
    modify_field_with_hash_based_offset(meta.hash, 0,
                                        flow_register_index, 65536);
    register_write(flow_to_port_map_register, meta.hash, 0);
}

action update_flow_count() {
    register_read(meta.temp, total_flow_count_register, meta.routing_port-2);
    add_to_field(meta.temp,-1);
    register_write(total_flow_count_register,meta.routing_port-2, meta.temp);
}

//--->Will be generated as _sync_2() in a separate file
action send_broadcast_update(){
    clone_ingress_pkt_to_egress(standard_metadata.egress_spec,meta_list);
    modify_field(load_balancer_head.preamble,2);
    modify_field(sync_info_head.flow_num,(@sum (1, SERVERPLUSONE, 1)(meta.server_flow$i));
    modify_field(intrinsic_metadata.mcast_grp, MCAST_GRP);
}

action _drop() {
    drop();
}

//------------------------ Control Logic -----------------

control ingress {
    apply(get_server_flow_count_table);

    //Preamble 1 => Probe packet
    if (load_balancer_head.preamble == 1){

        //Send update (set preamble as 2)
        @mirror(true,
                2,
                (@sum (1, SERVERPLUSONE, 1)(meta.server_flow$i)))
        //--->apply(send_update_table);
    }
    //Preamble 2 => Update packet
    else if (load_balancer_head.preamble == 2){
        //update the registers
        apply(update_switch_flow_count_table);
    }
    //Default Preamble is 0
    else {

        //-------------------------------------------------------------------------
        //Start of flow
        if(load_balancer_head.syn == 1) {

            //Forwarding to own server
            if(@bool (1,SERVERPLUSONE,1)(<)(and)(meta.server_flow$i, THRESHOLD)){
                //Forwarding can be done locally
                apply(set_server_dest_port_table);

                //Take decision to send probe packet or not (Reactive)
                if ((@sum (1, SERVERPLUSONE, 1)(meta.server_flow$i))*100 > (UPPER_LIMIT * SERVERS * THRESHOLD)){
                    apply(set_probe_bool_table);
                }
            }
            //Forwarding to another switch
            else{
                //Choose from switches
                apply(get_switch_flow_count_table);

                if (@bool (1,SWITCHES,1)(>=)(and)(meta.switch_flow$i, 2*THRESHOLD)){
                    apply(drop_table);
                }
                else {
                    @compare (SWITCHMINUSONE) (<=)
                    @for (i) (1,SWITCHES,1)
                        @case meta.switch_flow$i:
                            apply(set_switch$i_dest_port_table);
                        @endcase
                    @endfor
                    @endcompare
                }

            }

            //Remember mapping for the flow
            apply(update_map_table);
        }
        //-------------------------------------------------------------------------

        //Forwarding
        apply(forwarding_table);

        if (meta.routing_port == 0){
            apply(drop_table);
        }

        @sync(  meta.probe_bool == 1,
                1,
                MCAST_GRP)
        /*--->if(meta.probe_bool == 1){
            apply(send_probe_table);
        }*/

        //-------------------------------------------------------------------------
        //End of flow
        if(load_balancer_head.fin == 1) {

            //Clear mappings
            apply(clear_map_table);
            apply(update_flow_count_table);

            //Take decision to send probe packet or not (Proactive)
            @sync(
                ( @sum (1, SERVERPLUSONE, 1)(meta.server_flow$i))*100 < (LOWER_LIMIT * SERVERS * THRESHOLD) and (meta.routing_port == 2 or meta.routing_port == 3),
                    2,
                    MCAST_GRP,
                    (@sum (1, SERVERPLUSONE, 1)(meta.server_flow$i)))

            /*--->if ((@sum (1, 3, 1)(meta.server_flow$i))*100 < (LOWER_LIMIT * SERVERS * THRESHOLD)){
                if(meta.routing_port == 2 or meta.routing_port == 3){

                    //Send broadcast (set preamble as 2)
                    apply(send_broadcast_update_table);
                }
            }*/
        }
        //-------------------------------------------------------------------------
    }
}

control egress {
}
